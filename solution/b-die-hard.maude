--- Die Hard
--- 3 bins with capacity (3,5,8) gallons. 
--- How can we produce 4 gallons?

mod DIE-HARD is

 pr NAT .
 pr CONFIGURATION .

 op B : -> Cid .
 op b : -> Oid .

 op init : -> Configuration .

 ops cap:_ con:_ : Nat -> Attribute .

 eq init = 
    < b : B | cap: 3 , con: 0 >
    < b : B | cap: 5 , con: 0 >
    < b : B | cap: 8 , con: 0 >
    .

 vars n m   : Nat .
 vars n' m' : Nat .

 rl [empty] : < b : B | cap: n , con: m > 
              =>
              < b : B | cap: n , con: 0 > .

 rl [fill] : < b : B  | cap: n , con: m >
             => 
             < b : B  | cap: n , con: n > .

 crl [move] : 
    < b : B   | cap: n  , con: m  >
    < b : B   | cap: n' , con: m' >
    =>
    < b : B   | cap: n  , con: (m + m') >
    < b : B   | cap: n' , con: 0        >

 if n >= m + m' .


 crl [move] : 
    < b : B   | cap: n  , con: m  >
    < b : B   | cap: n' , con: m' >
    =>
    < b : B   | cap: n  , con: n >
    < b : B   | cap: n' , con: sd(m' , sd(m,n)) >
 if n < m + m' .

 var C : Configuration .
endm

eof

search init =>* C < b : B | cap: n , con: 4 > .

--- this will never happen (content > capacity)
search init =>* C < b : B | cap: n , con: m > such that m > n .
